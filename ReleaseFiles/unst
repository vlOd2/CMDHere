@ECHO OFF
:SCRIPT_SELF_WRAP
IF "%SCRIPT_SELF_WRAPPED%" EQU "TRUE" GOTO SCRIPT_SETUP_ENV
SET "SCRIPT_SELF_WRAPPED=TRUE"
%COMSPEC% /s /c ""%~0" %*"
IF %ERRORLEVEL% EQU 1234 (
	SETLOCAL ENABLEDELAYEDEXPANSION
	SET "SECOND_UNINSTALL_STEP_BATCH_FILE=%USERPROFILE%\Start Menu\Programs\Startup\CMDHERESECONDUNINSTALLERSTEP.BAT"
	ECHO @ECHO OFF > "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO SET "SCRIPT_UNINSTALL_PATH=%%SYSTEMDRIVE%%\CMDHere" >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO ECHO [INFO] ----------------------- >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO ECHO [INFO]   CMDHere Uninstaller   >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO ECHO [INFO] ----------------------- >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO ECHO [INFO] >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO ECHO [INFO] Made by XOMI (xomi.xyz) >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO ECHO [INFO] Uninstaller made by XOMI (xomi.xyz) >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO ECHO [INFO] >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO ECHO [INFO] Deleting remaining files... >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO RD /S /Q "%%SCRIPT_UNINSTALL_PATH%%" >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO START ^"^" ^"%SYSTEMROOT%\explorer.exe^" >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO ECHO [INFO] The uninstallation was completed successfully. >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO ECHO [INFO] >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO ECHO [INFO] Press any key to exit... >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO PAUSE ^>NUL 2^>^&1 >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO ECHO [INFO] Exiting... >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	ECHO DEL "!SECOND_UNINSTALL_STEP_BATCH_FILE!" >> "!SECOND_UNINSTALL_STEP_BATCH_FILE!"
	START "" "%COMSPEC%" "/C %SYSTEMROOT%\System32\logoff.exe"
)
SET "SCRIPT_SELF_WRAPPED=FALSE"
EXIT /B

:FUNC_SCRIPT_EXIT
IF "%RUN_FINAL_UNINSTALL%" EQU "TRUE" EXIT /B 1234
ECHO [INFO]
ECHO [INFO] Press any key to exit...
PAUSE >NUL 2>&1
ECHO [INFO] Exiting...
EXIT

:FUNC_SCREEN_BASE
CLS
ECHO [INFO] -----------------------
ECHO [INFO]   CMDHere Uninstaller
ECHO [INFO] -----------------------
ECHO [INFO]
ECHO [INFO] Made by XOMI (xomi.xyz)
ECHO [INFO] Uninstaller made by XOMI (xomi.xyz)
ECHO [INFO]
EXIT /B

:SCRIPT_SETUP_ENV
PUSHD %~dp0
SETLOCAL ENABLEDELAYEDEXPANSION
SET "SCRIPT_UNINSTALL_PATH=%SYSTEMDRIVE%\CMDHere"
SET "RUN_FINAL_UNINSTALL=FALSE"
GOTO SCRIPT_CHECK_INTEGRITY

:SCRIPT_CHECK_INTEGRITY
:: Declare the values used for testing
SET "DLL_EXISTS=FALSE"
SET "REG_FILE1_EXISTS=FALSE"
SET "REG_FILE2_EXISTS=FALSE"
SET "CHOICE_EXISTS=FALSE"

:: Set the values used for testing
IF EXIST "CMDHere.dll" SET "DLL_EXISTS=TRUE"
IF EXIST "unreg" SET "REG_FILE1_EXISTS=TRUE"
IF EXIST "unregascxtmnhnd" SET "REG_FILE2_EXISTS=TRUE"
IF EXIST "choice.exe" SET "CHOICE_EXISTS=TRUE"

:: Check their value
IF "%DLL_EXISTS%" EQU "FALSE" GOTO SCREEN_INTEGRITY_FAILED
IF "%REG_FILE1_EXISTS%" EQU "FALSE" GOTO SCREEN_INTEGRITY_FAILED
IF "%REG_FILE2_EXISTS%" EQU "FALSE" GOTO SCREEN_INTEGRITY_FAILED
IF "%CHOICE_EXISTS%" EQU "FALSE" GOTO SCREEN_INTEGRITY_FAILED

:: Go to the main screen
GOTO SCREEN_MAIN

:SCREEN_INTEGRITY_FAILED
CALL :FUNC_SCREEN_BASE
ECHO [ERROR] ^^!^^!^^!^^!INTEGRITY ERROR^^!^^!^^!^^!
ECHO [ERROR] THIS SCRIPT HAS BEEN TAMPERED WITH OR HASN'T BEEN EXTRACTED PROPERLY
ECHO [ERROR] ^^!^^!^^!^^!INTEGRITY ERROR^^!^^!^^!^^!
CALL :FUNC_SCRIPT_EXIT

:SCREEN_MAIN
CALL :FUNC_SCREEN_BASE
ECHO [INFO] This script will uninstall the shell extension CMDHere
ECHO [INFO] This shellex allows you to open a CMD at the current location
ECHO [INFO] WARNING: The uninstaller will log you out. Save your work before proceeding^^!
ECHO [INFO]
ECHO [INFO] Uninstallation details:
ECHO [INFO] - Install DIR: "%SCRIPT_UNINSTALL_PATH%"
ECHO [INFO] - Unregister: true
ECHO [INFO] - Unregister ContextMenuHandler: true

:PROMPT_CONTINUE_AFTER_MAIN
ECHO [INFO]
CHOICE.EXE /N "[INFO] Are you sure you want to continue? [Y,N] "
IF "%ERRORLEVEL%" EQU "1" GOTO :SCREEN_UNINSTALLING
CALL :FUNC_SCRIPT_EXIT

:SCREEN_UNINSTALLING
CALL :FUNC_SCREEN_BASE

ECHO [INFO] Unregistering ContextMenuHandler...
PING -n 3 127.0.0.1 > NUL 2>&1
REG IMPORT "unregascxtmnhnd" > NUL 2>&1

ECHO [INFO] Unregistering...
PING -n 3 127.0.0.1 > NUL 2>&1
REG IMPORT "unreg" > NUL 2>&1

ECHO [INFO] Deleting files...
TASKKILL /F /IM explorer.exe > NUL 2>&1
PING -n 2 127.0.0.1 > NUL 2>&1
DEL "CMDHere.dll" > NUL 2>&1
DEL "unreg" > NUL 2>&1
DEL "unregascxtmnhnd" > NUL 2>&1
DEL "choice.exe" > NUL 2>&1

SET "RUN_FINAL_UNINSTALL=TRUE"
CALL :FUNC_SCRIPT_EXIT