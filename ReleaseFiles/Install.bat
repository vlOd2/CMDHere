@ECHO OFF
:SCRIPT_SELF_WRAP
IF "%SCRIPT_SELF_WRAPPED%" EQU "TRUE" GOTO SCRIPT_SETUP_ENV
SET "SCRIPT_SELF_WRAPPED=TRUE"
%COMSPEC% /s /c ""%~0" %*"
SET "SCRIPT_SELF_WRAPPED=FALSE"
EXIT /B

:FUNC_SCRIPT_EXIT
ECHO [INFO]
ECHO [INFO] Press any key to exit...
PAUSE >NUL 2>&1
ECHO [INFO] Exiting...
EXIT

:FUNC_SCREEN_BASE
CLS
ECHO [INFO] ---------------------
ECHO [INFO]   CMDHere Installer
ECHO [INFO] ---------------------
ECHO [INFO]
ECHO [INFO] Made by vlOd
ECHO [INFO]
EXIT /B

:SCRIPT_SETUP_ENV
PUSHD %~dp0
SETLOCAL ENABLEDELAYEDEXPANSION
SET "SCRIPT_INSTALL_PATH=%SYSTEMDRIVE%\CMDHere"
GOTO SCRIPT_CHECK_INTEGRITY

:SCRIPT_CHECK_INTEGRITY
:: Declare the values used for testing
SET "DLL_EXISTS=FALSE"
SET "REG_FILE1_EXISTS=FALSE"
SET "REG_FILE2_EXISTS=FALSE"
SET "REG_FILE3_EXISTS=FALSE"
SET "REG_FILE4_EXISTS=FALSE"
SET "UNST_EXISTS=FALSE"
SET "CHOICE_EXISTS=FALSE"

:: Set the values used for testing
IF EXIST "CMDHere.dll" SET "DLL_EXISTS=TRUE"
IF EXIST "reg" SET "REG_FILE1_EXISTS=TRUE"
IF EXIST "regascxtmnhnd" SET "REG_FILE2_EXISTS=TRUE"
IF EXIST "unreg" SET "REG_FILE3_EXISTS=TRUE"
IF EXIST "unregascxtmnhnd" SET "REG_FILE4_EXISTS=TRUE"
IF EXIST "unst" SET "UNST_EXISTS=TRUE"
IF EXIST "choice.exe" SET "CHOICE_EXISTS=TRUE"

:: Check their value
IF "%DLL_EXISTS%" EQU "FALSE" GOTO SCREEN_INTEGRITY_FAILED
IF "%REG_FILE1_EXISTS%" EQU "FALSE" GOTO SCREEN_INTEGRITY_FAILED
IF "%REG_FILE2_EXISTS%" EQU "FALSE" GOTO SCREEN_INTEGRITY_FAILED
IF "%REG_FILE3_EXISTS%" EQU "FALSE" GOTO SCREEN_INTEGRITY_FAILED
IF "%REG_FILE4_EXISTS%" EQU "FALSE" GOTO SCREEN_INTEGRITY_FAILED
IF "%UNST_EXISTS%" EQU "FALSE" GOTO SCREEN_INTEGRITY_FAILED
IF "%CHOICE_EXISTS%" EQU "FALSE" GOTO SCREEN_INTEGRITY_FAILED

:: Go to the main screen
GOTO SCREEN_MAIN

:SCREEN_INTEGRITY_FAILED
CALL :FUNC_SCREEN_BASE
ECHO [ERROR] ^^!^^!^^!^^!INTEGRITY ERROR^^!^^!^^!^^!
ECHO [ERROR] THIS SCRIPT HAS BEEN TAMPERED WITH OR HASN'T BEEN EXTRACTED PROPERLY
ECHO [ERROR] ^^!^^!^^!^^!INTEGRITY ERROR^^!^^!^^!^^!
CALL :FUNC_SCRIPT_EXIT

:SCREEN_MAIN
CALL :FUNC_SCREEN_BASE
ECHO [INFO] This script will install the shell extension CMDHere
ECHO [INFO] This shellex allows you to open a CMD at the current location
ECHO [INFO]
ECHO [INFO] Installation details:
ECHO [INFO] - Install DIR: "%SCRIPT_INSTALL_PATH%"
ECHO [INFO] - Register: true
ECHO [INFO] - Register ContextMenuHandler: true

:PROMPT_CONTINUE_AFTER_MAIN
ECHO [INFO]
CHOICE.EXE /N "[INFO] Are you sure you want to continue? [Y,N] "
IF "%ERRORLEVEL%" EQU "1" GOTO :SCREEN_INSTALLING
CALL :FUNC_SCRIPT_EXIT

:SCREEN_INSTALLING
CALL :FUNC_SCREEN_BASE

ECHO [INFO] Creating the installation directory...
PING -n 2 127.0.0.1 > NUL 2>&1
MKDIR "%SCRIPT_INSTALL_PATH%" > NUL 2>&1

ECHO [INFO] Copying files...
PING -n 2 127.0.0.1 > NUL 2>&1
COPY /B /Y "CMDHere.dll" "%SCRIPT_INSTALL_PATH%\CMDHere.dll" > NUL 2>&1
COPY /B /Y "unreg" "%SCRIPT_INSTALL_PATH%\unreg" > NUL 2>&1
COPY /B /Y "unregascxtmnhnd" "%SCRIPT_INSTALL_PATH%\unregascxtmnhnd" > NUL 2>&1
COPY /B /Y "unst" "%SCRIPT_INSTALL_PATH%\Uninstall.bat" > NUL 2>&1
COPY /B /Y "choice.exe" "%SCRIPT_INSTALL_PATH%\choice.exe" > NUL 2>&1

ECHO [INFO] Registering...
PING -n 3 127.0.0.1 > NUL 2>&1
REG IMPORT "reg" > NUL 2>&1

ECHO [INFO] Registering ContextMenuHandler...
PING -n 3 127.0.0.1 > NUL 2>&1
REG IMPORT regascxtmnhnd > NUL 2>&1

ECHO [INFO]
ECHO [INFO] Press any key to continue...
PAUSE >NUL 2>&1

GOTO SCREEN_INSTALLATION_FINISHED

:SCREEN_INSTALLATION_FINISHED
CALL :FUNC_SCREEN_BASE
ECHO [INFO] The installation was completed successfully.
CALL :FUNC_SCRIPT_EXIT